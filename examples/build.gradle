apply plugin: 'com.android.application'

android {
    compileSdkVersion rootProject.compileSdk

    defaultConfig {
        applicationId "org.rajawali3d.examples"
        minSdkVersion 16
        targetSdkVersion rootProject.targetSdk
        versionCode rootProject.versionCode
        versionName rootProject.versionName
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
        }
    }

    flavorDimensions "feature"
    productFlavors {
        withVuforia {
            minSdkVersion 19
            dimension "feature"
        }
        withoutVuforia {
            dimension "feature"
        }
    }

    lintOptions {
        abortOnError false
    }
    packagingOptions {
        pickFirst 'lib/armeabi-v7a/libvraudio_engine.so'
        pickFirst 'lib/armeabi-v7a/libvrtoolkit.so'
    }

    dexOptions {
        // Skip pre-dexing when running on CI
        preDexLibraries = !isCi
        javaMaxHeapSize maxDexHeap
    }
}

dependencies {
    implementation "com.android.support:support-v13:$supportLib"
    implementation "com.android.support:appcompat-v7:$supportLib"
    implementation "com.android.support:recyclerview-v7:$supportLib"
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'com.koushikdutta.ion:ion:2.1.7'

    implementation project(':rajawali')
    withVuforiaImplementation project(':vuforia')
    withVuforiaImplementation project(':vr')

    wearApp project(':wear-example')
}
